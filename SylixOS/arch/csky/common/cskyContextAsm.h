;/*********************************************************************************************************
;**
;**                                    中国软件开源组织
;**
;**                                   嵌入式实时操作系统
;**
;**                                       SylixOS(TM)
;**
;**                               Copyright  All Rights Reserved
;**
;**--------------文件信息--------------------------------------------------------------------------------
;**
;** 文   件   名: cskyContextAsm.h
;**
;** 创   建   人: Wang.Xuan (王Q)
;**
;** 文件创建日期: 2018 年 05 月 11 日
;**
;** 描        述: C-SKY 体系架构上下文处理.
;*********************************************************************************************************/

#ifndef __ARCH_CSKYCONTEXTASM_H
#define __ARCH_CSKYCONTEXTASM_H

#include "arch/csky/arch_regs.h"

;/*********************************************************************************************************
;  保存寄存器(参数 A1: ARCH_REG_CTX 地址)
;*********************************************************************************************************/

MACRO_DEF(SAVE_REGS)
    STM         R0-R31 , (A1)

    ST.W        RA , (A1 , XPC)                                         ;/*  RA 代替 PC 保存             */
    
    MFCR        A2 , PSR
    ST.W        A2 , (A1 , XPSR)                                        ;/*  保存 PSR 寄存器             */

    MFLO        A2                                                      ;/*  保存 LO 寄存器              */
    ST.W        A2 , (A1 , XLO)

    MFHI        A2                                                      ;/*  保存 HI 寄存器              */
    ST.W        A2 , (A1 , XHI)

    MFCR        A2 , CR<4, 15>                                          ;/*  保存 MEH 寄存器             */
    ST.W        A2 , (A1 , XMEH)
    MACRO_END()

;/*********************************************************************************************************
;  恢复寄存器(参数 A1: ARCH_REG_CTX 地址)
;*********************************************************************************************************/

MACRO_DEF(RESTORE_REGS)
    LD.W        A0 , (A1 , XLO)                                         ;/*  恢复 LO 寄存器              */
    MTLO        A0

    LD.W        A0 , (A1 , XHI)                                         ;/*  恢复 HI 寄存器              */
    MTHI        A0

    LD.W        A0 , (A1 , XMEH)                                        ;/*  恢复 MEH 寄存器             */
    MTCR        A0 , CR<4, 15>

    LD.W        A0 , (A1 , XPSR)                                        ;/*  恢复 PSR 寄存器             */
    MTCR        A0 , EPSR

    LD.W        A0 , (A1 , XPC)                                         ;/*  恢复 PC 寄存器              */
    MTCR        A0 , EPC

    LD.W        R0 , (A1 , XGREG(0))

    ADDI        A1 , 8
    LDM         R2-R31 , (A1)
    SUBI        A1 , 8
    LD.W        A1 , (A1 , XGREG(1))

    RTE
    MACRO_END()

#endif
;/*********************************************************************************************************
;  END
;*********************************************************************************************************/
